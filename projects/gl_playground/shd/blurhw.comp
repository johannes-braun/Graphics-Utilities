#ifndef KERNEL
#define KERNEL_SIZE 15
#define KERNEL 0.105915,0.102673,0.093531,0.080066,0.064408,0.048689,0.034587,0.023089,
#endif

#define KERNEL_HALF (KERNEL_SIZE >> 1)
#ifndef DIRECTION
#define DIRECTION 1
#endif

layout(local_size_x = 16, local_size_y = 16) in;

layout(binding = 0) uniform sampler2D in_texture;
layout(binding = 1, rgba8) restrict writeonly uniform image2D out_image;

const float kernel[] = { KERNEL };

void main()
{
    const ivec2 tex_size = imageSize(out_image);
    const ivec2 pixel = ivec2(gl_GlobalInvocationID.xy);

    vec2 offset = vec2(0);
    vec4 color = textureLod(in_texture, (pixel + offset) / vec2(tex_size), 0) * kernel[0];
    for(int i=0; i<KERNEL_HALF; ++i)
    {
        offset[DIRECTION] = 2.0f*(i + 1);
        color += textureLod(in_texture, (pixel - offset) / vec2(tex_size), 0) * kernel[i + 1];
        color += textureLod(in_texture, (pixel + offset) / vec2(tex_size), 0) * kernel[i + 1];
    }
    
    imageStore(out_image, pixel, color);
}